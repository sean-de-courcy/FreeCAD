/*

uninstall.nsh

Uninstall

*/

Var FileAssociation

# ----------------------------------

Section "un.FreeCAD" un.SecUnProgramFiles

  SectionIn RO

  # delete start menu folder
  ReadRegStr $0 SHCTX "${APP_UNINST_KEY}" "StartMenu"
  RMDir /r "$0"
  # delete desktop icon
  Delete "$DESKTOP\${APP_NAME}.lnk"

  # FreeCAD-CH: remove only what this installer wrote (configure.nsh). The .FCStd key, the
  # other extensions, Explorer's per-user choices and the thumbnail handler belong to
  # official FreeCAD.
  ReadRegStr $R0 SHCTX "Software\Classes\${APP_EXT}" ""
  ${if} $R0 == "${APP_REGNAME_DOC}"
   DeleteRegValue SHCTX "Software\Classes\${APP_EXT}" ""
  ${endif}
  DeleteRegValue SHCTX "Software\Classes\${APP_EXT}\OpenWithProgids" "${APP_REGNAME_DOC}"
  DeleteRegKey SHCTX "Software\Classes\${APP_REGNAME_DOC}"

  # Uninstaller itself
  Delete "$INSTDIR\${SETUP_UNINSTALLER}"

  # Application folder
  SetOutPath "$TEMP"
  DetailPrint "Uninstalling files from '$INSTDIR'"
  SetDetailsPrint textonly
  RMDir /r "$INSTDIR"
  SetDetailsPrint both

  # Registry keys and values
  DeleteRegKey SHCTX "${APP_REGKEY_SETUP}"
  DeleteRegKey SHCTX "${APP_REGKEY}"
  DeleteRegKey SHCTX "${APP_UNINST_KEY}"
  DeleteRegKey HKCR "Applications\${BIN_FREECAD}"
  DeleteRegValue HKCR "${APP_NAME}.Document\Shell\open\command" ""
  DeleteRegValue HKCR "${APP_NAME}.Document\DefaultIcon" ""

  # File associations
  ReadRegStr $FileAssociation SHELL_CONTEXT "Software\Classes\${APP_EXT}" ""

  ${If} $FileAssociation == "${APP_REGNAME_DOC}"
     DeleteRegValue SHELL_CONTEXT "Software\Classes\${APP_EXT}" ""
  ${EndIf}

  # clean other registry entry
  DeleteRegKey SHCTX "SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\${APP_NAME}.exe"

  # Eventually refresh shell icons
   ${RefreshShellIcons}

SectionEnd

#---------------------------------
# user preferences
Section /o "un.$(UnFreeCADPreferencesTitle)" un.SecUnPreferences

 # issue a warning dialog
 MessageBox MB_YESNO|MB_DEFBUTTON2|MB_ICONEXCLAMATION $(DialogUnPreferences) /SD IDYES IDYES +2 # continue if yes
  Goto NotPreferences
 # remove FreeCAD's config files
 StrCpy $AppSubfolder ${APP_DIR_USERDATA}
 Call un.DelAppPathSub # function from Utils.nsh
 # remove the registry key that stores the main window parameters
 DeleteRegKey HKCU "SOFTWARE\${APP_NAME}"
 NotPreferences:

SectionEnd

#---------------------------------
# Section descriptions
!insertmacro MUI_UNFUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${un.SecUnPreferences} "$(SecUnPreferencesDescription)"
!insertmacro MUI_DESCRIPTION_TEXT ${un.SecUnProgramFiles} "$(SecUnProgramFilesDescription)"
!insertmacro MUI_UNFUNCTION_DESCRIPTION_END
